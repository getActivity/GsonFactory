//文件开头配置
apply plugin: 'com.android.library'
apply plugin: 'com.novoda.bintray-release'

android {
    compileSdkVersion 26

    defaultConfig {
        minSdkVersion 11
        targetSdkVersion 26
        versionCode 30
        versionName "3.0"
    }
}

dependencies {
    // Json 解析框架：https://github.com/google/gson
    // noinspection GradleDependency
    implementation 'com.google.code.gson:gson:2.8.0'
}

publish {
    userOrg = 'getactivity'
    groupId = 'com.hjq.gson'
    artifactId = 'factory'
    publishVersion = '3.0'
    desc = 'Gson parsing fault-tolerant framework'
    website = "https://github.com/getActivity/GsonFactory"
}

// 防止编码问题
tasks.withType(Javadoc) {
    options.addStringOption('Xdoclint:none', '-quiet')
    options.addStringOption('encoding', 'UTF-8')
    options.addStringOption('charSet', 'UTF-8')
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives javadocJar
    archives sourcesJar
}